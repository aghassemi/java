buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'wrapper'

android {
    buildToolsVersion '21.1.2'
    compileSdkVersion 19

    defaultConfig {
        minSdkVersion 19
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

repositories {
    mavenCentral()

    maven {
        url "http://jring.mtv:8081/nexus/content/repositories/test_repo/"
    }
}

dependencies {
    compile 'io.v:vanadium:0.1-SNAPSHOT'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.google.truth:truth:0.25'
    // This dependency exists only to work around an issue in the sdkmanager plugin v0.12.0. This
    // should be fixed when http://git.io/vIXec is checked in and a release is made.
    androidTestCompile('com.android.support:support-v4:21.0.0')
    androidTestCompile('com.android.support.test:runner:0.3') {
        exclude group: 'junit'  // junit:junit-dep conflicts with junit:unit
    }
}

task cleanNativeLibOutputs(type: Delete) {
    delete 'src/main/jniLibs/armeabi-v7a/libv23.h'
}

task buildNativeLib(type: Exec, dependsOn: cleanNativeLibOutputs) {
    def v23Root = System.getenv('V23_ROOT')
    if (v23Root == null) {
        throw new InvalidUserDataException('V23_ROOT environment variable is not set, did you follow the Vanadium installation instructions?')
    }
    def oldPath = System.getenv('PATH')
    if (oldPath == null) {
        oldPath = ''
    }
    environment 'GOOS', 'android'
    environment 'GOARCH', 'arm'
    environment 'GOARM', '7'
    environment 'PATH', v23Root + '/third_party/android/go/bin:' + oldPath
    commandLine v23Root + '/devtools/bin/v23', 'go', 'build', '-o', 'src/main/jniLibs/armeabi-v7a/libv23.so', '-buildmode=c-shared', '-v', '-a', '-tags', 'android', 'v.io/x/jni'
}

clean {
    delete 'src/main/jniLibs/armeabi-v7a/libv23.so'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        androidLibrary(MavenPublication) {
            groupId 'io.v'
            artifactId 'vanadium-android'
            version '0.1-SNAPSHOT'
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/android-lib-release.aar")
        }
    }
    repositories {
        maven {
            credentials {
                username 'admin'
                password 'admin123'
            }
            url "http://jring.mtv:8081/nexus/content/repositories/test_repo"
        }
    }
}

wrapper {
    gradleVersion = '2.4'
}

tasks.'preBuild'.dependsOn(buildNativeLib)
tasks.'publish'.dependsOn(tasks.'assemble')
