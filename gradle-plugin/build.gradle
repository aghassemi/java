apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'wrapper'

repositories {
    mavenCentral()
}

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile 'com.google.truth:truth:0.27'
}

class VdlPlatform {
    final String name
    final String goOs
    final String goArch

    VdlPlatform(String name, String goOs, String goArch) {
        this.name = name
        this.goOs = goOs
        this.goArch = goArch
    }
}

def platforms = [new VdlPlatform("linux", "linux", "amd64"),
                 new VdlPlatform("macosx", "darwin", "amd64")]

def pomDependencies = new Node(null, 'dependencies')

platforms.each({ platform ->
    task "buildVdl${platform.name}"(type: Exec) {
        description 'Build the VDL tool'
        group 'Build'
        environment GOOS: platform.goOs
        environment GOARCH: platform.goArch
        commandLine 'jiri', 'go', 'build', '-o', "build/vdltool/vdl-${platform.name}",
                'v.io/x/ref/cmd/vdl'
    }

    task "natives-${platform.name}"(type: Jar, dependsOn: "buildVdl${platform.name}") {
        from 'build/vdltool/vdl-' + platform.name
    }

    tasks.'processResources'.dependsOn("buildVdl${platform.name}")

    def dependencyNode = new XmlParser().parseText("""
<dependency>
    <groupId>io.v</groupId>
    <artifactId>gradle-plugin-vdltool-${platform.name}</artifactId>
    <version>0.1-SNAPSHOT</version>
</dependency>
""")
    pomDependencies.append(dependencyNode)
})

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'io.v'
            artifactId 'gradle-plugin'
            version '0.1-SNAPSHOT'

            from components.java

            pom.withXml {
                asNode().append(pomDependencies)
            }
        }

        platforms.each({ platform ->
            "mavenNatives${platform.name}"(MavenPublication) {
                groupId 'io.v'
                artifactId "gradle-plugin-vdltool-${platform.name}"
                version '0.1-SNAPSHOT'

                artifact project.tasks."natives-${platform.name}"
            }
        })
    }
    repositories {
        maven {
            credentials {
                username 'admin'
                password 'admin123'
            }
            url 'http://srdjan.mtv:8081/nexus/content/repositories/test_repo'
        }
    }
}

wrapper {
    gradleVersion = '2.4'
}
